(ns sample.hero-animations
  "Faithful port of https://docs.flutter.dev/cookbook/navigation/hero-animations"
  (:require ["package:flutter/material.dart" :as m]
            [cljd.flutter :as f]))

(def detail-screen
  (m/Scaffold
    .body
    (f/widget
      :get [m/Navigator]
      (m/GestureDetector .onTap #(.pop navigator))
      m/Center
      (m/Hero .tag "imageHero")
      (m/Image.network "https://picsum.photos/250?image=9"))))

(def main-screen
  (m/Scaffold
    .appBar (m/AppBar .title (m/Text "Main Screen"))
    .body
    (f/widget :get [m/Navigator]
      (m/GestureDetector
        .onTap (fn []
                 (.push navigator (#/(m/MaterialPageRoute Object)
                                    .builder (f/build detail-screen)))
                 nil))
      (m/Hero .tag "imageHero")
      (m/Image.network "https://picsum.photos/250?image=9"))))

(defn main
  []
  (m/runApp
   (m/MaterialApp
    .home main-screen
    .debugShowCheckedModeBanner false)))
